!!ARBvp1.0
#
# simple vertex shader to setup data for per-pixel specular lighting
#
# on entry:
#      vertex.position
#      vertex.normal       - normal vector (n) of TBN basic
#      vertex.texcoord [0] - normal texture coordinates
#      vertex.texcoord [1] - tangent vector  (t)
#      vertex.texcoord [2] - binormal vector (b)
#
#      program.local [0] - eye position
#      program.local [1] - light position
#
#      state.matrix.program [0] - rotation matrix for the object
#
# on exit:
#     result.texcoord [0] - texture coordinates
#     result.texcoord [1] - v
#

#	We assume that object whose vertices are passed are transformed by program [0] matrix
#	Instead of transforming vertex and TBN basis with this matrix we transform light pos
#	and eye pos with inverse matrix to leave TBN intact.
#	This way we keep l, v and h vectors correct
#

ATTRIB	pos     = vertex.position;
PARAM	eye     = program.local [0];
PARAM	light   = program.local [1];
PARAM	mvp [4] = { state.matrix.mvp };
PARAM	mv0 [4] = { state.matrix.program [0].inverse };		# inverse rotation matrix
PARAM	half    = 0.5;
PARAM	step    = 0.2;

TEMP	l, l2, v, v2, temp;
TEMP	vt, et;

			# transform eye position
DP4	et.x, eye, mv0 [0];
DP4	et.y, eye, mv0 [1];
DP4	et.z, eye, mv0 [2];
DP4	et.w, eye, mv0 [3];

			# compute v (vector to viewer)
ADD	v, -pos, et;			# v = eye - pos

			# normalize it (we need to correctly compute h)
DP3	temp.x, v, v;				# now temp.x = (v,v)
RSQ	temp.y, temp.x;				# compute inverse square root of (v,v)
MUL	v, v, temp.y;				# normalize

			# transform it into tangent space
DP3	vt.x, v, vertex.texcoord [1];
DP3	vt.y, v, vertex.texcoord [2];
DP3	vt.z, v, vertex.normal;
MOV	vt.w, v.w;

			# store it into texcoord [2]
MOV	result.texcoord [1], vt;

			# store texcoord [0]
MOV	result.texcoord [0], vertex.texcoord [0];

			# copy primary and secondary colors
MOV	result.color,           vertex.color;
MOV	result.color.secondary, vertex.color.secondary;

MUL	temp, step, vertex.normal;
ADD	temp, temp, vertex.position;
MOV	temp.w, vertex.position.w;

			# transform position into clip space
DP4	result.position.x, temp, mvp [0];
DP4	result.position.y, temp, mvp [1];
DP4	result.position.z, temp, mvp [2];
DP4	result.position.w, temp, mvp [3];

			# we're done
END
